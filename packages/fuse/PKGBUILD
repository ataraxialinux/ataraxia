# Maintainer: nee-san <nagakamira@gmail.com>

pkgbase=fuse
pkgver=3.4.1
pkgname=('fuse-common' 'fuse')
pkgrel=1
arch=('x86_64' 'x86' 'arm64' 'armv7l' 'armv5tel' 'ppc64le' 'ppc64' 'ppc')
url="https://github.com/libfuse/libfuse"
license=('GPL-2.0-only LGPL-2.1-only')
makedepends=('meson')
options=('!emptydirs')
source=("https://github.com/libfuse/libfuse/releases/download/$pkgbase-$pkgver/$pkgbase-$pkgver.tar.xz"
	'fuse.conf'
	'initd')
md5sums=('68eac68b19997bea69c4d94f110003f0'
         'c9457cf5b2196da67d5ac816d1c86a4f'
         'e10fff8c7b1ed1369af644f2a0e3b65b')

prepare() {
	cd "$srcdir"/$pkgbase-$pkgver
	rm -rf build && mkdir -p build
}

build() {
	cd "$srcdir"/$pkgbase-$pkgver/build
	meson "$srcdir"/$pkgbase-$pkgver \
		--prefix / \
		--sbindir bin
	ninja
}

package_fuse-common() {
	pkgdesc="Common files for fuse2/3 packages"
	backup=('etc/fuse.conf')
	depends=('musl')

	cd "$srcdir"/$pkgbase-$pkgver/build
	DESTDIR="$pkgdir" ninja install

	install -Dm644 "$srcdir"/fuse.conf "$pkgdir"/etc/fuse.conf

	rm -r "$pkgdir"/dev

	rm -r "$pkgdir"/etc/init.d

	rm -r "$pkgdir"/{bin/fusermount3,include,lib/{pkgconfig,libfuse3.so*},share/man/man1/fusermount3.1}

	mv "$pkgdir"/bin/mount.fuse3 "$pkgdir"/bin/mount.fuse

	install -Dm755 "$srcdir"/initd "$pkgdir"/etc/init.d/fuse
}

package_fuse() {
	pkgdesc="A library that makes it possible to implement a filesystem in a userspace program."
	depends=('fuse-common')

	cd "$srcdir"/$pkgbase-$pkgver/build
	DESTDIR="$pkgdir" ninja install

	rm -r "$pkgdir"/etc/init.d

	rm "$pkgdir"/etc/fuse.conf

	rm -r "$pkgdir"/dev

	rm -rf "$pkgdir"/lib/udev/rules.d/99-fuse3.rules

	rm -rf "$pkgdir"/usr
	rm "$pkgdir"/share/man/man8/mount.fuse3.8 "$pkgdir"/bin/mount.fuse3
}
