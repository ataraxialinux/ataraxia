# Maintainer: nee-san <nagakamira@gmail.com>

pkgname=linux-api-headers
pkgver=4.20.6
pkgrel=1
pkgdesc="Kernel headers sanitized for use in userspace"
arch=('x86_64' 'x86' 'arm64' 'armv7l' 'armv5tel' 'ppc64le' 'ppc64' 'ppc')
url="https://www.kernel.org/"
license=('GPL-2.0-only')
source=("https://cdn.kernel.org/pub/linux/kernel/v4.x/linux-$pkgver.tar.xz")
md5sums=('43cb8498de6830c9ed8021d3ab52741c')

build() {
	cd "$srcdir"/linux-$pkgver
	make mrproper
}

package() {
	case $CARCH in
		x86_64)
			export XKARCH="x86_64"
			;;
		x86)
			export XKARCH="i386"
			;;
		arm64)
			export XKARCH="arm64"
			;;
		armv7l|armv5tel)
			export XKARCH="arm"
			;;
		ppc64le|ppc64|ppc)
			export XKARCH="powerpc"
			;;
		*)
			echo "Architecture is not set or is not supported by 'bootstrap' script"
			exit 1
	esac

	cd "$srcdir"/linux-$pkgver
	make ARCH=$XKARCH INSTALL_HDR_PATH="$pkgdir" headers_install

	rm -r "$pkgdir"/include/drm

	find "$pkgdir" \( -name .install -o -name ..install.cmd \) -delete
}
