# Maintainer: nee-san <nagakamira@gmail.com>

pkgname=mdadm
pkgver=4.1
pkgrel=1
pkgdesc="A tool for managing/monitoring Linux md device arrays, also known as Software RAID"
arch=('x86_64' 'x86' 'arm64' 'armv7l' 'armv5tel' 'ppc64le' 'ppc64' 'ppc')
url="http://neil.brown.name/blog/mdadm"
license=('GPL-2.0-only')
depends=('musl')
backup=('etc/mdadm.conf')
source=("https://www.kernel.org/pub/linux/utils/raid/$pkgname/$pkgname-$pkgver.tar.xz"
	'disable-werror.patch'
	'mdadm.confd'
	'mdadm.initd'
	'mdraid.confd'
	'mdraid.initd'
	'mdadm.conf')
md5sums=('51bf3651bd73a06c413a2f964f299598'
         '599745ed2bec4489e83991cff89c46ee'
         '6682657bc391e56742065e9aecc2306f'
         '107097201fab3386055bdb6ed1710da2'
         'bbda899e5193aa5e8c498dcdf5736775'
         '3af0f9428d5e03359507c3c2bc65e44e'
         '6962e425245957c3754ba7a8469b71ac')

build() {
	cd "$srcdir"/$pkgname-$pkgver
	make CXFLAGS="$CFLAGS" PREFIX='' BINDIR=/bin UDEVDIR=/lib/udev
}

package() {
	cd "$srcdir"/$pkgname-$pkgver
	make PREFIX='' BINDIR=/bin UDEVDIR=/lib/udev DESTDIR="$pkgdir" install

	install -D -m644 "$srcdir"/mdadm.conf "$pkgdir"/etc/mdadm.conf

	for initd in mdadm mdraid; do
		install -D -m755 "$srcdir"/${initd}.initd "$pkgdir"/etc/init.d/$initd
	done

	for confd in mdadm mdraid; do
		install -D -m644 "$srcdir"/${confd}.confd "$pkgdir"/etc/conf.d/$confd
	done

	mv "$pkgdir"/usr/* "$pkgdir"/
	rm -rf "$pkgdir"/usr
}
