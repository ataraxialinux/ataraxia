# Maintainer: nee-san <nagakamira@gmail.com>

pkgname=aspell
_pkgver=0.60.7-rc1
pkgver=${_pkgver/-/}
_pkgmajorver=0.60
pkgrel=1
pkgdesc="A spell checker designed to eventually replace Ispell"
arch=('x86_64' 'x86' 'arm64' 'armv7l' 'armv5tel' 'ppc64le' 'ppc64' 'ppc')
url="http://aspell.net/"
license=('LGPL-2.0-or-later')
depends=('gcc-runtime' 'ncurses')
optdepends=('perl: to import old dictionaries')
source=("https://github.com/GNUAspell/aspell/archive/rel-$_pkgver.tar.gz")
md5sums=('d0a766260a7877d1f06ed9aac74db5d5')

prepare() {
	cd "$srcdir"/$pkgname-rel-$_pkgver
	PERL_USE_UNSAFE_INC=1 ./autogen
}

build() {
	cd "$srcdir"/$pkgname-rel-$_pkgver
	./configure \
		--prefix='' \
		--sysconfdir=/etc \
		--build=$CHOST
	make
}

package() {
	cd "$srcdir"/$pkgname-rel-$_pkgver
	make DESTDIR="$pkgdir" install

	ln -sf $pkgname-$_pkgmajorver "$pkgdir"/lib/$pkgname
}
