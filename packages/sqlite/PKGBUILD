# Maintainer: nee-san <nagakamira@gmail.com>

pkgname=sqlite
pkgver=3.27.0
_a=${pkgver%%.*}
_b=${pkgver#${_a}.}
_b=${_b%%.*}
_c=${pkgver#${_a}.${_b}.}
_c=${_c%%.*}
case $pkgver in
	*.*.*.*)_d=${pkgver##*.};;
	*.*.*)	_d=0;;
esac
[ $_b -lt 10 ] && _b=0$_b
[ $_c -lt 10 ] && _c=0$_c
[ $_d -lt 10 ] && _d=0$_d
ver=${_a}${_b}${_c}${_d}
pkgrel=1
pkgdesc="A C library that implements an SQL database engine"
arch=('x86_64' 'x86' 'arm64' 'armv7l' 'armv5tel' 'ppc64le' 'ppc64' 'ppc')
url="http://www.sqlite.org/"
license=('Public-Domain')
depends=('musl' 'libz' 'libedit')
options=('!emptydirs')
source=("http://www.sqlite.org/2019/$pkgname-autoconf-$ver.tar.gz")
md5sums=('339fdefddac48c9f145cc2b71822e407')

amalgamation="-DSQLITE_ENABLE_FTS4 \
	-DSQLITE_ENABLE_FTS3_PARENTHESIS \
	-DSQLITE_ENABLE_FTS3 \
	-DSQLITE_ENABLE_FTS5 \
	-DSQLITE_ENABLE_COLUMN_METADATA \
	-DSQLITE_SECURE_DELETE \
	-DSQLITE_ENABLE_UNLOCK_NOTIFY \
	-DSQLITE_ENABLE_RTREE \
	-DSQLITE_ENABLE_GEOPOLY \
	-DSQLITE_USE_URI \
	-DSQLITE_ENABLE_DBSTAT_VTAB \
	-DSQLITE_MAX_VARIABLE_NUMBER=250000 \
	-DSQLITE_ENABLE_JSON1"

build() {
	cd "$srcdir"/$pkgname-autoconf-$ver
	CFLAGS="$CFLAGS $amalgamation" \
	./configure \
		--prefix='' \
		--enable-dynamic-extensions \
		--enable-fts3 \
		--enable-readline \
		--enable-static \
		--enable-threadsafe \
		--build=$CHOST

	sed -i 's|^hardcode_libdir_flag_spec=.*|hardcode_libdir_flag_spec=""|g' libtool
	sed -i 's|^runpath_var=LD_RUN_PATH|runpath_var=DIE_RPATH_DIE|g' libtool

	make
}

package() {
	cd "$srcdir"/$pkgname-autoconf-$ver
	make DESTDIR="$pkgdir" install
}
