# Maintainer: nee-san <nagakamira@gmail.com>

pkgname=p7zip
pkgver=16.02
pkgrel=1
pkgdesc="Command-line file archiver with high compression ratio"
arch=('x86_64' 'x86' 'arm64' 'armv7l' 'armv5tel' 'ppc64le' 'ppc64' 'ppc')
url="http://p7zip.sourceforge.net/"
license=('LGPL-2.0-or-later')
depends=('gcc-runtime' 'bash')
makedepends=('nasm' 'yasm')
source=("https://downloads.sourceforge.net/project/$pkgname/$pkgname/$pkgver/${pkgname}_${pkgver}_src_all.tar.bz2"
	'CVE-2016-9296.patch'
	'CVE-2017-17969.patch'
	'CVE-2018-5996.patch'
	'CVE-2018-10115.patch')
md5sums=('a0128d661cfe7cc8c121e73519c54fbf'
         '2fd6b8aefbe4d2900b167f75ce6988ed'
         '32360b69c89cf499cd3392df3e3fd423'
         'c41e8e6e7f201084e2d22ddc53014d46'
         '9dcbf7c934b1def32ba659df3ce8b89c')

prepare() {
	cd "$srcdir"/${pkgname}_$pkgver
	patch -Np1 -i "$srcdir"/CVE-2016-9296.patch
	patch -Np1 -i "$srcdir"/CVE-2017-17969.patch
	patch -Np1 -i "$srcdir"/CVE-2018-5996.patch
	patch -Np1 -i "$srcdir"/CVE-2018-10115.patch

	local makefile="makefile.linux_any_cpu_gcc_4.X"
	case "$CARCH" in
		x86)    makefile="makefile.linux_x86_asm_gcc_4.X" ;;
		x86_64) makefile="makefile.linux_amd64_asm" ;;
	esac

	ln -sf $makefile makefile.machine
}

build() {
	cd "$srcdir"/${pkgname}_$pkgver
	make all3 OPTFLAGS="$CFLAGS"
}

package() {
	cd "$srcdir"/${pkgname}_$pkgver
	make install \
		DEST_DIR="$pkgdir" \
		DEST_HOME='' \
		DEST_MAN=/share/man
}
