# Maintainer: nee-san <nagakamira@gmail.com>

pkgname=texinfo
pkgver=6.5
pkgrel=1
pkgdesc="GNU documentation system for on-line information and printed output"
arch=('x86_64' 'x86' 'arm64' 'armv7l' 'armv5tel' 'ppc64le' 'ppc64' 'ppc')
url="http://www.gnu.org/software/texinfo/"
license=('GPL-3.0+')
depends=('musl' 'ncurses' 'perl' 'bash' 'gzip')
source=("http://ftpmirror.gnu.org/gnu/$pkgname/$pkgname-$pkgver.tar.xz"
		'r8006.patch'
		'texinfo-install.hook'
		'texinfo-remove.hook')
md5sums=('3715197e62e0e07f85860b3d7aab55ed'
         'aca223e0b876f94181910c37f8f650e0'
         '4da5ca1eb9b7168c09f486aedb1c0dbb'
         'd92ab17bbc72efc52bf2544b6b5cad7e')

prepare() {
	cd "$srcdir"/$pkgname-$pkgver
	patch -p1 -i "$srcdir"/r8006.patch
}

build() {
	cd "$srcdir"/$pkgname-$pkgver
	./configure \
		--prefix='' \
		--build=$CHOST
	make
}

package() {
	cd "$srcdir"/$pkgname-$pkgver
	make DESTDIR="$pkgdir" install

	install -dm755 "$pkgdir"/share/libalpm/hooks/
	install -m644 "$srcdir"/texinfo-{install,remove}.hook "$pkgdir"/share/libalpm/hooks/
}
