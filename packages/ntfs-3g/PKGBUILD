# Maintainer: nee-san <nagakamira@gmail.com>

pkgname=ntfs-3g
_pkgname=ntfs-3g_ntfsprogs
pkgver=2017.3.23
pkgrel=1
pkgdesc="NTFS filesystem driver and utilities"
arch=('x86_64' 'x86' 'arm64' 'armv7l' 'armv5tel' 'ppc64le' 'ppc64' 'ppc')
url="https://www.tuxera.com/community/open-source-ntfs-3g/"
license=('GPL2')
depends=('musl' 'util-linux' 'fuse2')
source=("https://tuxera.com/opensource/${_pkgname}-${pkgver}.tgz"
	'mftmirr.patch')
md5sums=('d97474ae1954f772c6d2fa386a6f462c'
         'd50ad7c8b1911dfd1dc48441f877f7b7')

prepare() {
	cd "$srcdir"/${_pkgname}-${pkgver}
	sed 's|$(DESTDIR)/sbin|$(DESTDIR)/bin|' -i {ntfsprogs,src}/Makefile.in
	patch -Np1 -i "$srcdir"/mftmirr.patch
}

build() {
	cd "$srcdir"/${_pkgname}-${pkgver}
	./configure \
		--prefix='' \
		--sbin=/bin \
		--mandir=/share/man \
		--with-fuse=external \
		--enable-extras \
		--enable-posix-acls \
		--enable-xattr-mappings \
		--disable-ldconfig \
		--disable-static \
		--build=$CHOST
	make
}

package() {
	cd "$srcdir"/${_pkgname}-${pkgver}
	make DESTDIR="$pkgdir" rootbindir=/bin rootsbindir=/bin rootlibdir=/lib install

	ln -sf /bin/ntfs-3g "$pkgdir"/bin/mount.ntfs
}
