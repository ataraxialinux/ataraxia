# Maintainer: nee-san <nagakamira@gmail.com>

pkgname=ccache
pkgver=3.5
pkgrel=1
pkgdesc="Compiler cache that speeds up recompilation by caching previous compilations"
arch=('x86_64' 'x86' 'arm64' 'armv7l' 'armv5tel' 'ppc64le' 'ppc64' 'ppc')
url="https://ccache.samba.org/"
license=('GPL-3.0-or-later')
depends=('musl' 'libz')
source=("https://www.samba.org/ftp/$pkgname/$pkgname-$pkgver.tar.xz")
md5sums=('340d688072070bf1fd61d41688e69a19')

build() {
	cd "$srcdir"/$pkgname-$pkgver
	./configure \
		--prefix='' \
		--sysconfdir=/etc \
		--build=$CHOST
	make
}

package() {
	cd "$srcdir"/$pkgname-$pkgver
	install -Dm 755 ccache -t "$pkgdir"/bin

	install -d "$pkgdir"/lib/ccache/bin
	local _prog
	for _prog in gcc g++ c++; do
		ln -sf /bin/ccache "$pkgdir"/lib/ccache/bin/$_prog
		ln -sf /bin/ccache "$pkgdir"/lib/ccache/bin/${CHOST}-$_prog
	done
	for _prog in cc clang clang++; do
		ln -sf /bin/ccache "$pkgdir"/lib/ccache/bin/$_prog
	done
}
