# Maintainer: nee-san <nagakamira@gmail.com>

pkgbase=python-six
pkgname=('python2-six' 'python3-six')
pkgver=1.12.0
pkgrel=2
pkgdesc="Python 2 and 3 compatibility utilities"
arch=('any')
url="http://pypi.python.org/pypi/six/"
license=('MIT')
makedepends=('python' 'python2')
source=("https://pypi.io/packages/source/s/six/six-$pkgver.tar.gz")
md5sums=('9ae5d1feed8c0215f4ae4adcd9207fcb')

build() {
	cd "$srcdir"
	cp -a six-$pkgver{,-py2}
}

package_python3-six() {
	depends=('python3')

	cd "$srcdir"/six-$pkgver
	python3 setup.py install --prefix='' --root "$pkgdir" --optimize=1
}

package_python2-six() {
	depends=('python2')

	cd "$srcdir"/six-$pkgver-py2
	python2 setup.py install --prefix='' --root "$pkgdir" --optimize=1
}
