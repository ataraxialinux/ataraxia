# Maintainer: nee-san <nagakamira@gmail.com>

pkgname=binutils
pkgver=2.32
pkgrel=2
pkgdesc="Tools necessary to build programs"
arch=('x86_64' 'x86' 'arm64' 'armv7l' 'armv5tel' 'ppc64le' 'ppc64' 'ppc')
url="https://www.gnu.org/software/binutils/"
license=('GPL-2.0 GPL-3.0-or-later LGPL-2.0 BSD')
depends=('musl' 'libz')
options=('!distcc' '!ccache')
source=("http://ftpmirror.gnu.org/gnu/$pkgname/$pkgname-$pkgver.tar.xz"
	'gold-mips.patch')
md5sums=('0d174cdaf85721c5723bf52355be41e6'
         '030ab638472f037a57f0d6ec839e39b0')

prepare() {
	cd "$srcdir"/$pkgname-$pkgver
	patch -Np1 -i "$srcdir"/gold-mips.patch

	mkdir build
}

build() {
	if [ "$CARCH" = "x86_64" ]; then
		export BINUTILSOPTS="--enable-targets=x86_64-pep"
	fi

	cd "$srcdir"/$pkgname-$pkgver
	cd build
	../configure \
		--prefix='' \
		--build=$CHOST $BINUTILSOPTS \
		--with-bugurl="https://github.com/JanusLinux/janus/issues" \
		--with-lib-path="/lib:/local/lib" \
		--with-pic \
		--with-system-zlib \
		--enable-64-bit-bfd \
		--enable-deterministic-archives \
		--enable-gold=yes \
		--enable-ld=default \
		--enable-lto \
		--enable-plugins \
		--enable-relro \
		--enable-shared \
		--enable-threads \
		--disable-multilib \
		--disable-nls \
		--disable-werror
	make MAKEINFO="true" tooldir='' configure-host
	make MAKEINFO="true" tooldir=''
}

package() {
	cd "$srcdir"/$pkgname-$pkgver
	cd build
	make MAKEINFO="true" tooldir='' DESTDIR="$pkgdir" install
}
