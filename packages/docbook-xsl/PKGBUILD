# Maintainer: nee-san <nagakamira@gmail.com>

pkgname=docbook-xsl
pkgver=1.79.2
pkgrel=1
pkgdesc="XML stylesheets for Docbook-xml transformations"
arch=('any')
url="http://docbook.org/"
license=('custom')
depends=('libxml2' 'libxslt' 'docbook-xml' 'sed')
install=$pkgname.install
source=("https://github.com/docbook/xslt10-stylesheets/releases/download/release%2F$pkgver/docbook-xsl-nons-$pkgver.tar.gz"
	'765567_non-recursive_string_subst.patch')
md5sums=('ca2a6aa27de09ed676d5fc0c9385cbba'
         '4cb5c9bc0e39516921f56f57301341b0')

prepare() {
	cd "$srcdir"/$pkgname-nons-$pkgver
	patch -Np2 -i "$srcdir"//765567_non-recursive_string_subst.patch
}


package() {
	cd "$srcdir"/$pkgname-nons-$pkgver
	_pkgroot="$pkgdir"/share/xml/docbook/xsl-stylesheets-$pkgver

	install -dm755 ${_pkgroot}
	install -m644 VERSION VERSION.xsl ${_pkgroot}

	for fn in assembly common eclipse epub epub3 fo highlighting html htmlhelp javahelp lib manpages params profiling roundtrip template website xhtml xhtml-1_1 xhtml5; do
		install -dm755 ${_pkgroot}/${fn}
		install -m644 ${fn}/*.{xml,xsl,dtd,ent} ${_pkgroot}/${fn} || true  # ignore missing files
	done

	install -dm755 "$pkgdir"/etc/xml
}
